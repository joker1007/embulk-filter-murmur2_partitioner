plugins {
    id "org.embulk.embulk-plugins" version "0.5.5"
    id "java"
    id "checkstyle"
}

repositories {
    mavenCentral()
}

group = "org.embulk.filter.murmur2_partitioner"
version = "0.3.0"
description = "Murmur2 Partitioner filter plugin for Embulk"

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {

    compileOnly "org.embulk:embulk-spi:0.11"
    implementation("org.apache.kafka:kafka-clients:2.8.1") {
        exclude group: "org.slf4j", module: "slf4j-api"
    }

    implementation("org.embulk:embulk-util-config:0.3.4") {
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        exclude group: "javax.validation", module: "validation-api"
    }

    implementation "com.fasterxml.jackson.core:jackson-annotations:2.6.7"
    implementation "com.fasterxml.jackson.core:jackson-core:2.6.7"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.6.7"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7"

    testImplementation "junit:junit:4.+"
    testImplementation "org.embulk:embulk-spi:0.11"
    testImplementation "org.embulk:embulk-core:0.11.0"
    testImplementation "org.embulk:embulk-junit4:0.11.0"
    testImplementation "org.embulk:embulk-input-file:0.11.0"
    testImplementation "org.embulk:embulk-output-file:0.11.0"
    testImplementation "org.embulk:embulk-parser-csv:0.11.0"
    testImplementation "org.embulk:embulk-formatter-csv:0.11.0"
    testImplementation "org.embulk:embulk-deps:0.11.0"
    testImplementation "org.hamcrest:hamcrest:2.2"

    testImplementation "com.salesforce.kafka.test:kafka-junit4:3.2.3"
    testImplementation("org.apache.kafka:kafka_2.12:2.8.+") {
        exclude group: "org.slf4j", module: "slf4j-api"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.dataformat", module: "jackson-dataformat-csv"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        exclude group: "com.fasterxml.jackson.module", module: "jackson-module-scala_2.12"
        exclude group: "com.fasterxml.jackson.module", module: "jackson-module-paranamer"
    }
}

embulkPlugin {
    mainClass = "org.embulk.filter.murmur2_partitioner.Murmur2PartitionerFilterPlugin"
    category = "filter"
    type = "murmur2_partitioner"
}

test {
    minHeapSize = "512m"
    maxHeapSize = "4g"
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

gem {
    from("LICENSE.txt")
    authors = [ "joker1007" ]
    email = [ "kakyoin.hierophant@gmail.com" ]
    description = "Murmur2 Partitioner filter plugin for Embulk."
    summary = "Murmur2 Partitioner filter plugin for Embulk"
    homepage = "https://github.com/joker1007/embulk-filter-murmur2_partitioner"
    licenses = [ "MIT" ]
}

gemPush {
    host = "https://rubygems.org"
}
